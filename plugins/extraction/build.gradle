group "org.polypheny"


dependencies {
    compileOnly project(":core")
    compileOnly project(":plugins:sql-language")
    compileOnly project(":plugins:cypher-language")

    implementation group: "org.glassfish", name: "jakarta.json", version: jakarta_json_version
    implementation(group: "io.javalin", name: "javalin", version: javalin_version) {
        exclude group: 'org.slf4j'
    } // Apache 2.0

    // --- Test Compile ---
    testImplementation project(path: ":core", configuration: "tests")
    testImplementation project(path: ":core")
    testImplementation group: "junit", name: "junit", version: junit_version
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java"]
            outputDir = file(project.buildDir.absolutePath + "/classes")
        }
        resources {
            srcDirs = ["src/main/resources"]
        }
        output.resourcesDir = file(project.buildDir.absolutePath + "/classes")
    }
    test {
        java {
            srcDirs = ["src/test/java"]
            outputDir = file(project.buildDir.absolutePath + "/test-classes")
        }
        resources {
            srcDirs = ["src/test/resources"]
        }
        output.resourcesDir = file(project.buildDir.absolutePath + "/test-classes")
    }
}

compileJava {
    dependsOn(":config:processResources")
    dependsOn(":core:processResources")
    dependsOn(":information:processResources")
    dependsOn(":plugins:sql-language:processResources")
    dependsOn(":plugins:cypher-language:processResources")
}

delombok {
    dependsOn(":plugins:sql-language:processResources")
    dependsOn(":plugins:cypher-language:processResources")
}


/**
 * JARs
 */
jar {
    manifest {
        attributes "Manifest-Version": "1.0"
        attributes "Copyright": "The Polypheny Project (polypheny.org)"
        attributes "Version": "$project.version"
    }
}
java {
    withJavadocJar()
    withSourcesJar()
}

licensee {
    allow('Apache-2.0')
    allowUrl('https://www.eclipse.org/org/documents/epl-v10.php') // EPL 1.0
    allowDependency('javax.servlet', 'javax.servlet-api', '3.1.0') {
        because 'Servlet-api.jar and javax.servlet-*.jar are under the CDDL license, the original source code for this can be found at http://www.eclipse.org/jetty/downloads.php'
    }
    allowDependency('org.glassfish', 'jakarta.json', '2.0.1') { because 'Eclipse Public License v. 2.0' }
}

